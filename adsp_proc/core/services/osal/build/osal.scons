#===============================================================================
#
# OSAL Libs
#
# GENERAL DESCRIPTION
#    build script
#
# Copyright (c) 2009-2015, 2017 by QUALCOMM Technologies, Incorporated.
# All Rights Reserved.
# QUALCOMM Proprietary/GTDR
#
#-------------------------------------------------------------------------------
#
#  $Header: //source/qcom/qct/core/bsp/config/msm7x30/main/latest/boot/appsproc/build/msm7x30.sconscript#3 $
#  $DateTime: 2009/07/19 21:27:25 $
#  $Author: wduembeg $
#  $Change: 971778 $
#                      EDIT HISTORY FOR FILE
#
#  This section contains comments describing changes made to the module.
#  Notice that changes are listed in reverse chronological order.
#
# when       who     what, where, why
# --------   ---     ---------------------------------------------------------
# 10/05/17   is      Separated out a file for uimage
# 04/27/17   sp      Added RCINIT_IMG for SLPI, ADSP, CDSP rootPD and userPD
# 04/28/15   vk      Added osal_init dependency, and added this dependency into 
#                    audio image.
# 10/09/14   rs      Updated scons api's.
# 05/16/14   sm      Added TISE image support.  Note that 'USES_MSVC' and
#                    'USES_TISE' are used to specify a subset of files from this
#                    driver to compile for the Windows (MSVC) compiler.  Please
#                    do not remove these definitions from this file.
#10/21/13    ph      OSAL APIs updated use ATS timers.
#===============================================================================
import os
Import('env')
env = env.Clone()

# This is to remove any -Werror declarations in the environmnent for the MSVC
# compiler.
if 'USES_MSVC' in env:
    env.Replace(CFLAGS= '-Dinline="__inline"')
    env.Append(CPPDEFINES = [
    'CUST_H=custaaaaaaaaq.h',
    '__FILENAME__=__FILE__',
    ])

#-------------------------------------------------------------------------------
# Source PATH
#-------------------------------------------------------------------------------

SRCPATH = "${BUILD_ROOT}/core/services/osal/qurt/src"
 
env.VariantDir('${BUILDPATH}', SRCPATH, duplicate=0)

#-------------------------------------------------------------------------------
# Internal depends within CoreBSP
#-------------------------------------------------------------------------------
CBSP_API = [
   'SERVICES',
   'MPROC',
   'SYSTEMDRIVERS',
   'DEBUGTOOLS',
   'DAL',
   
   # needs to be last also contains wrong comdef.h
   'KERNEL',
]

env.RequirePublicApi(CBSP_API)
env.RequireRestrictedApi(CBSP_API)

#-------------------------------------------------------------------------------
# Sources, libraries
#-------------------------------------------------------------------------------

OSAL_SOURCES = [
   '${BUILDPATH}/osal_qurt.c',
]

OSAL_SOURCES_SHARED = [
   '${BUILDPATH}/osal_qurt_uimage.c',
]


#-------------------------------------------------------------------------------
# Add Libraries to image
#-------------------------------------------------------------------------------

# CORE_Q6_ROOT is equivalent to CORE_SLPI_ROOT, CORE_ADSP_ROOT, CORE_CDSP_ROOT
# There's currently no user pd for CDSP
OSAL_ALL_IMGS = ['CORE_Q6_ROOT',
                 'CORE_SLPI_USER', 'CORE_ADSP_USER']
                        
env.AddLibrary(OSAL_ALL_IMGS, '${BUILDPATH}/osal', OSAL_SOURCES)

if 'USES_ISLAND' in env:
  osal_shared = env.AddLibrary(OSAL_ALL_IMGS, '${BUILDPATH}/osal_shared', OSAL_SOURCES_SHARED)
  env.AddIslandLibrary(OSAL_ALL_IMGS, osal_shared)
else:
  env.AddLibrary(OSAL_ALL_IMGS, '${BUILDPATH}/osal_shared', OSAL_SOURCES_SHARED)
  
#-------------------------------------------------------------------------------
# RCINIT Task and Init Fields and Initialization
#-------------------------------------------------------------------------------
RCINIT_INIT_OSAL_INIT = {
  'sequence_group'             : 'RCINIT_GROUP_0',        # required
  'init_name'                  : 'osal',                  # required
  'init_function'              : 'osal_init',             # required
  'dependencies'               : ['timer_client']
}
 
#-------------------------------------------------------------------------------
# Adding proper RootPD and UserPD build tag to RCINIT_IMG results in
# osal_init() being autogenerated in rcinit_autogen.c to be called during bootup 
#-------------------------------------------------------------------------------
   
if 'USES_RCINIT' in env:
   env.AddRCInitFunc( OSAL_ALL_IMGS, RCINIT_INIT_OSAL_INIT )

   
