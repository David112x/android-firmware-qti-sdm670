#===============================================================================
#
# Settings Registrations
#
# GENERAL DESCRIPTION
#    build script
#
# Copyright (c) 2017 by QUALCOMM Technologies, Incorporated.
# All Rights Reserved.
# QUALCOMM Proprietary/GTDR
#
#-------------------------------------------------------------------------------
#
#  $Header: //components/rel/core.qdsp6/2.1/settings/systemdrivers/GPIOInt/build/GPIOInt.scons#1 $
#  $DateTime: 2017/07/21 22:10:47 $
#  $Author: pwbldsvc $
#  $Change: 13907704 $
#                      EDIT HISTORY FOR FILE
#
#  This section contains comments describing changes made to the module.
#  Notice that changes are listed in reverse chronological order.
#
# when       who     what, where, why
# --------   ---     ---------------------------------------------------------
# 02/13/17   dcf     Initial version.
#===============================================================================
import os
Import('env')

# The LLVM compiler added in several dozen -Wunused-function warnings with entries
# in device configuration.  We will suppress these warnings for the devcfg_img
# SCons image only.
env.Append(CFLAGS = ' -Wno-unused-function')

#-------------------------------------------------------------------------------
# Source PATH
#-------------------------------------------------------------------------------
SRCPATH = "../config"

env.VariantDir('${BUILDPATH}', SRCPATH, duplicate=0)

#-------------------------------------------------------------------------------
# External depends within CoreBSP
#-------------------------------------------------------------------------------
CBSP_API = [
  'DAL',
  'SERVICES',
  'SYSTEMDRIVERS',
  'GPIOINT_DEVCFG_INTERFACE',
  'KERNEL',
]

#-------------------------------------------------------------------------------
# Image Definitions
#-------------------------------------------------------------------------------
GPIOINT_USER_IMAGES = ['CORE_ADSP_USER', 'CORE_SSC_ADSP_USER', 'CORE_AVS_ADSP_USER']
GPIOINT_KERNEL_IMAGES = ['CORE_ADSP_ROOT', 'CORE_SLPI_ADSP_ROOT']

#-------------------------------------------------------------------------------
# Publish Private APIs
#-------------------------------------------------------------------------------

env.RequireProtectedApi(['HAL'])
env.RequirePublicApi(CBSP_API)
env.RequireRestrictedApi(CBSP_API)


if 'USES_DEVCFG' in env:
  image_path = os.path.join(os.getcwd(),'..','config',env['IMAGE'],'kernel',env['CHIPSET'])
  file_list = []
  devcfg_key = ''

  if os.path.exists(image_path):
    file_list.append('${CORE_SETTINGS_DIR}/systemdrivers/GPIOInt/config/${IMAGE}/kernel/${CHIPSET}/GPIOInt.xml')
    file_list.append('${BUILDPATH}/${IMAGE}/kernel/${CHIPSET}/GPIOInt_ConfigData.c')
    devcfg_key = '{}_xml'.format(env['MSM_ID'])
  else:
    file_list.append('${CORE_SETTINGS_DIR}/systemdrivers/GPIOInt/config/GPIOIntStub.xml')
    file_list.append('${BUILDPATH}/GPIOInt_ConfigDataStub.c')
    devcfg_key = 'soc_xml'

  DEVCFG_IMG = ['DAL_DEVCFG_IMG', 'DEVCFG_CORE_QDSP6_SENSOR_SW', 'DEVCFG_CORE_QDSP6_AUDIO_SW']
  env.AddDevCfgInfo(DEVCFG_IMG, 
  {
     devcfg_key : file_list
  })

#---------------------------------------------------------------------------
# Pack out target specific files not needed
#---------------------------------------------------------------------------

# Remove target config kernel files
ALL_KERNEL_CONFIG_FILES = env.FindFiles(
  ['*'],
  '../config/kernel/')
MY_KERNEL_CONFIG_FILES = env.FindFiles(
  ['*'],
  '../config/kernel/${CHIPSET}')
PACK_KERNEL_CONFIG_FILES = list(set(ALL_KERNEL_CONFIG_FILES) - set(MY_KERNEL_CONFIG_FILES))
env.CleanPack(GPIOINT_KERNEL_IMAGES, PACK_KERNEL_CONFIG_FILES)

# Remove target config user files
ALL_USER_CONFIG_FILES = env.FindFiles(
  ['*'],
  '../config/user/')
MY_USER_CONFIG_FILES = env.FindFiles(
  ['*'],
  '../config/user/${CHIPSET}')
PACK_USER_CONFIG_FILES = list(set(ALL_USER_CONFIG_FILES) - set(MY_USER_CONFIG_FILES))
env.CleanPack(GPIOINT_USER_IMAGES, PACK_USER_CONFIG_FILES)


