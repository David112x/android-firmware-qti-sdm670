#===============================================================================
#
# CHRE Location driver dynamic builder scons
#
# GENERAL DESCRIPTION
#    Build script
#
# Copyright (c) 2017 by QUALCOMM Technologies, Incorporated.
# All Rights Reserved.
# QUALCOMM Proprietary/GTDR
#
#-------------------------------------------------------------------------------

Import('env')
import os

#-------------------------------------------------------------------------------
# Setup source PATH
#-------------------------------------------------------------------------------
LIB_ROOT = "${BUILD_ROOT}/chre/chre_drv_loc"
SRCPATH = LIB_ROOT + '/src'
LIBNAME = 'chre_drv_loc'
env.VariantDir('${BUILDPATH}', SRCPATH, duplicate=0)

target_so = '${BUILDPATH}/'+LIBNAME+'.so'
hexagon_root = env.get('HEXAGON_ROOT')
hexgaon_rtos_release = env.get('HEXAGON_RTOS_RELEASE')
hexagon_version = env.get('Q6VERSION')
HEXAGON_LIB_PATH = "{0}/{1}/Tools/target/hexagon/lib/{2}/G0/pic".format(hexagon_root,hexgaon_rtos_release,hexagon_version)
#-------------------------------------------------------------------------------
# Setup env Flags
#-------------------------------------------------------------------------------
env.Replace(ASM_DFLAGS = '-D__V_DYNAMIC__')
env.Replace(CC_DFLAGS = '-D__V_DYNAMIC__')
env.Replace(HEXAGONCC_OPT = ' -O0 ')
env.Replace(HEXAGONCC_WARN = '-Wall -Wno-cast-align -Wpointer-arith -Wno-missing-braces -Wno-strict-aliasing -Wstrict-prototypes -Wnested-externs')

env.Replace(HEXAGON_UNDEF = ' ')
env.Replace(HEXAGON_LANIND = '-fno-exceptions -fno-strict-aliasing -fno-zero-initialized-in-bss -fdata-sections')    
env.Replace(CFLAGS = ' -G0 ')
env.Append(SHLINKFLAGS = '-G0 --wrap=malloc --wrap=calloc --wrap=free --wrap=realloc --wrap=memalign --wrap=__stack_chk_fail -call_shared ')
env.Append(SHLINKFLAGS = ' -L{} '.format(HEXAGON_LIB_PATH))

env['SKEL_DEP'] = ' ${BUILD_ROOT}/chre/chre_pal/build/${BUILDPATH}/chre_pal.so '

env.Replace(QDSP6OBJS1= '${QDSP6_RELEASE_DIR}/Tools/target/hexagon/lib/${Q6VERSION}/G0/pic/initS.o')
env.Replace(QDSP6OBJS3= ['${QDSP6_RELEASE_DIR}/Tools/target/hexagon/lib/${Q6VERSION}/G0/pic/libgcc.a',
                             '${QDSP6_RELEASE_DIR}/Tools/target/hexagon/lib/${Q6VERSION}/G0/pic/libstdc++.a',])
env.Replace(QDSP6OBJS4= '${QDSP6_RELEASE_DIR}/Tools/target/hexagon/lib/${Q6VERSION}/G0/pic/finiS.o')

env['SHLINKGRP'] = ' -shared -fPIC -Bsymbolic --start-group $QDSP6OBJS1 $QDSP6OBJS3 $SOURCES.posix $QDSP6OBJS4 $SKEL_DEP --end-group ${HEXAGONLD_MAP_CMD} ${TARGET.posix}.map'

env.Replace(SHLINKCOM="${TEMPFILE('$SHLINK $SHLINKFLAGS $HEXAGON_OUTPUT_CMD $TARGET.posix $SHLINKGRP')}")

env.Depends(target_so, '${BUILD_ROOT}/chre/chre_pal/build/${BUILDPATH}/chre_pal.so')
#-------------------------------------------------------------------------------
# Get the required Include directories
#-------------------------------------------------------------------------------
core_public_apis = [
       'SERVICES',
       'DEBUGTOOLS',
       'ADSPPM',
       'POWER',
       'MPROC',
       'SYSTEMDRIVERS',
       'DEBUGTRACE',
       'DAL',
       'DIAG',
       'BUSES',
]

platform_public_apis = [
       'PLATFORM_LIBS',
       'MOD_TABLE',
       'QAIC',
       'STDDEF',
       'REMOTE',
       'HAPSDK',
]

qmimsgs_public_apis = [
       'COMMON',
       'LOWI',
]

env.PublishPrivateApi('CHRE_DRV_LOC',[
         "${BUILD_ROOT}/chre/chre_drv_loc/inc",
         "${BUILD_ROOT}/chre/chre_drv_loc/src",
         "${BUILD_ROOT}/chre/chre_drv_loc/src/core",
         "${BUILD_ROOT}/chre/chre_drv_loc/src/loc_api/libloc_api_50001",
         "${BUILD_ROOT}/chre/chre_drv_loc/src/loc_api/loc_api_v02",
         "${BUILD_ROOT}/chre/chre_drv_loc/src/utils",
         "${BUILD_ROOT}/chre/chre/src/system/chre/chre_api/include/chre_api",
         "${BUILD_ROOT}/chre/chre/src/system/chre/chre_api/include",
         "${BUILD_ROOT}/chre/chre/src/system/chre/pal/include",
         "${BUILD_ROOT}/chre/chre/src/system/chre/pal/include/chre/pal",
])

env.RequirePublicApi(['KERNEL'], area='CORE')
env.RequirePublicApi(qmimsgs_public_apis, area='qmimsgs')
env.RequirePublicApi(core_public_apis, area='core')
env.RequirePublicApi(platform_public_apis, area='platform')
env.RequirePrivateApi('CHRE_DRV_LOC')
#-------------------------------------------------------------------------------
# Add CHRE LOC specific Define
#-------------------------------------------------------------------------------
CHRE_DRV_LOC_DEFINES = ['CHRE_MESSAGE_TO_HOST_MAX_SIZE=4000']

env.Append(CPPDEFINES = CHRE_DRV_LOC_DEFINES)

#-------------------------------------------------------------------------------
# Setup source files
#-------------------------------------------------------------------------------
chre_drv_loc_src = [
                        "${BUILDPATH}/loc_api/libloc_api_50001/Loc.cpp",
                        "${BUILDPATH}/loc_api/libloc_api_50001/LocAdapter.cpp",
                        "${BUILDPATH}/utils/LocThread.cpp",
                        "${BUILDPATH}/utils/loc_log.cpp",
                        "${BUILDPATH}/utils/MsgTask.cpp",
                        "${BUILDPATH}/core/LocApiBase.cpp",
                        "${BUILDPATH}/core/LocAdapterBase.cpp",
                        "${BUILDPATH}/core/ContextBase.cpp",
                        "${BUILDPATH}/core/LocDualContext.cpp",
                        "${BUILDPATH}/loc_api/loc_api_v02/LocApiV02.cpp",
                        "${BUILDPATH}/utils/msg_q.c",
                        "${BUILDPATH}/utils/linked_list.c",
                        "${BUILDPATH}/utils/pthread_loc.c",
                        "${BUILDPATH}/loc_api/loc_api_v02/location_service_v02.c",
                        "${BUILDPATH}/loc_api/loc_api_v02/loc_api_sync_req.c",
                        "${BUILDPATH}/loc_api/loc_api_v02/loc_api_v02_client.c",
                        "${BUILDPATH}/loc_api/loc_api_v02/loc_api_v02_log.c",
                  ]

#-------------------------------------------------------------------------------
# Call the build command
#-------------------------------------------------------------------------------
env.AddBinarySharedLibrary(['ADSP_SHARED_LIBS'], '${BUILDPATH}/'+LIBNAME, chre_drv_loc_src)

# Clean / pack rules
CLEAN_LIST=[]
CLEAN_LIST.extend(env.FindFiles(['*.h', '*.hpp', '*.c', '*.cpp', '*.map','*.os'], '${BUILDPATH}'))
CLEAN_LIST.extend(env.FindFiles(['*.cmd'], LIB_ROOT))
CLEAN_LIST.extend(env.FindFiles(['*'], LIB_ROOT + '/inc'))
CLEAN_LIST.extend(env.FindFiles(['*'], LIB_ROOT + '/src'))
CLEAN_LIST.extend(env.FindFiles(['*'], LIB_ROOT + '/test'))

env.CleanPack(['ADSP_SHARED_LIBS'], CLEAN_LIST) 

	
